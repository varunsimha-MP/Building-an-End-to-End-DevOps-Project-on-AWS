resource "aws_iam_role" "eks_efs_pod_identity_role" {
  name = "Terraform_AmazonEKSPodIdentityAmazonEFSCSIDriverRole"

  assume_role_policy = jsonencode({
    
    "Version": "2012-10-17",
    "Statement": [
        {
            "Effect": "Allow",
            "Principal": {
                "Service": "pods.eks.amazonaws.com"
            },
            "Action": [
                "sts:AssumeRole",
                "sts:TagSession"
            ]
        }
    ]

  })
}

resource "aws_iam_role_policy_attachment" "eks_efs_policy" {
  role       = aws_iam_role.eks_efs_pod_identity_role.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AmazonEFSCSIDriverPolicy"

}

resource "aws_eks_pod_identity_association" "eks_efs_csi_driver" {
  cluster_name    = aws_eks_cluster.main_eks.name
  namespace       = "kube-system"
  service_account = "efs-csi-controller-sa"
  role_arn        = aws_iam_role.eks_efs_pod_identity_role.arn
  depends_on = [ 
    aws_eks_cluster.main_eks,
    aws_eks_addon.pod_identity_agent
   ]
}

resource "aws_eks_addon" "eks_efs_csi_driver" {
  cluster_name = aws_eks_cluster.main_eks.name
  addon_name = "amazon-efs-csi-driver"
  service_account_role_arn = aws_iam_role.eks_efs_pod_identity_role.arn
  addon_version = "v2.1.8-eksbuild.1"

  depends_on = [
    aws_eks_pod_identity_association.eks_efs_csi_driver,
    aws_eks_cluster.main_eks
  ]
}